#@TYPE: Machine
#@NAME: Teledyne QLS1046A-4GB on Scalys development board
#@SOC: LSCH2
#@DESCRIPTION: Machine configuration for running LS1046A in 64-bit mode
#@MAINTAINER: opensource@scalys.com

MACHINEOVERRIDES =. "fsl-lsch2:ls1046a:"

require conf/machine/include/qoriq-arm64.inc
require conf/machine/include/arm/arch-arm64.inc

KERNEL_CLASSES  = " kernel-fitimage "
KERNEL_IMAGETYPES = "fitImage"

UBOOT_DTB_LOADADDRESS = "0x90000000"
UBOOT_ENTRYPOINT = "0x80080000"

UBOOT_CONFIG ??= "nor sdhc"
UBOOT_CONFIG[sdhc] = "qls1046a_4gb_sdcard_defconfig,,u-boot-with-spl-pbl.bin"
UBOOT_CONFIG[nor] = "qls1046a_4gb_ifc_nor_defconfig,,u-boot-with-spl-pbl.bin"

KERNEL_DEVICETREE ?= "\
    freescale/qls1046a-4gb-sdk.dtb \
"
# Additional .config fragments are added in the kernel recipe
KERNEL_DEFCONFIG ?= "defconfig"

SERIAL_CONSOLES ?= "115200;ttyS0 115200;ttyAMA0 115200;ttyLP0"
SERIAL_CONSOLES_CHECK ?= "${SERIAL_CONSOLES}"

EXTRA_IMAGEDEPENDS += "fm-ucode"
USE_VT = "0"

# Images types generated for this machine
IMAGE_FSTYPES = "tar.bz2 ubifs ubi"

# Undo UBI configuration in /meta-qoriq-demos/conf/distro/fsl-qoriq.conf
OVERRIDES =. "ubiargs-override:"
# mkfs.ubi options: 
# Find parameter values on target with `ubinfo -a`
#    -m, --min-io-size=SIZE   minimum I/O unit size
#    -e, --leb-size=SIZE      logical erase block size
#    -c, --max-leb-cnt=COUNT  maximum logical erase block count
MKUBIFS_ARGS_ubiargs-override ?= "--space-fixup -m 2048 -e 124KiB -c 10000"

# ubinize options:
# Find parameter values on target with `mtdinfo -a`
# -p, --peb-size=<bytes>       size of the physical eraseblock of the flash
# -m, --min-io-size=<bytes>    minimum input/output unit size of the flash in bytes
# -s, --sub-page-size=<bytes>  minimum input/output unit used for UBI headers
UBINIZE_ARGS_ubiargs-override ?= "-m 2048 -p 128KiB -s 2048"
UBI_VOLNAME = "rootfs"

# Hide (yet) unsupported recipes for this board
BBMASK += "/meta-qoriq-demos/recipes-bsp/edgescale-bootstrap/edgescale-bootstrap.bb"
BBMASK += "/meta-qoriq-demos/recipes-bsp/secure-boot/sequre-boot-qoriq.bb"

MACHINE_FEATURES += "optee"
OPTEEMACHINE = "ls1046ardb"
